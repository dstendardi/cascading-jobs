apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.viadeo.cascading'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    mavenRepo name: 'datanucleus', url: 'http://www.datanucleus.org/downloads/maven2'
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
    mavenRepo name: 'sonatype', url: 'http://oss.sonatype.org/content/groups/public/'
    mavenRepo name: 'sonatype-snapshots', url: 'http://oss.sonatype.org/content/repositories/snapshots'
    mavenRepo name: 'cloudera-releases', url: 'https://repository.cloudera.com/content/repositories/releases'
}

ext.cascadingVersion = '2.1.2'

configurations {
    all*.exclude group: 'org.mortbay.jetty'
    all*.exclude group: 'tomcat'
    all*.exclude group: 'org.apache.hive'
    all*.exclude module: 'slf4j-log4j12'
    cucumberRuntime {
        exclude group: 'commons-httpclient'
        extendsFrom testRuntime
    }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '14.0.1'
    compile group: 'com.twitter', name: 'maple', version: '0.2.7'
    compile(group: 'org.apache.hbase', name: 'hbase', version: '0.92.1-cdh4.1.2') {
        exclude group: 'com.sun.jersey'
    }

    compile group: 'org.apache.hadoop', name: 'hadoop-client', version: '2.0.0-mr1-cdh4.2.0'

    compile group: 'org.apache.avro', name: 'avro', version: '1.7.4'
    compile group: 'org.apache.avro', name: 'avro-mapred', version: '1.7.4'

    // the production code uses the slf4j logging api at compile time
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'cascading', name: 'cascading-core', version: '2.1.2'
    compile group: 'cascading', name: 'cascading-local', version: '2.1.2'
    compile group: 'cascading', name: 'cascading-hadoop', version: '2.1.2'
    compile (group: 'org.elasticsearch', name: 'elasticsearch-hadoop', version:'1.3.0.BUILD-SNAPSHOT') {
        exclude group: 'org.apache.pig'
    }

    compile group: 'cascading.avro', name: 'avro-scheme', version: '2.1.1'

    compile group: 'commons-daemon', name: 'commons-daemon', version: '1.0.15'

    cucumberRuntime files("${jar.archivePath}")

    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.0.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'info.cukes', name: 'cucumber-java', version: '1.1.2'
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.1.2'
    testCompile group: 'info.cukes', name: 'cucumber-picocontainer', version: '1.1.2'
    testCompile group: 'org.elasticsearch', name: 'elasticsearch', version: '0.20.6'
}

sourceSets {
    main {
        output.resourcesDir = 'build/classes/main/resources'
        output.classesDir   = 'build/classes/main/java'
    }
    test {
        output.resourcesDir = 'build/classes/test/resources'
        output.classesDir   = 'build/classes/test/java'
    }
}

jar {
    description = "Assembles a Hadoop ready jar file"
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes("Main-Class": "com.viadeo.cascading/Criteria")
    }
}


task cucumber() {
    dependsOn assemble, compileTestJava, processTestResources
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.test.runtimeClasspath
            args = ["-f", "pretty", "--glue", "com.viadeo.cascading.steps", "src/test/resources"]
        }
    }
}