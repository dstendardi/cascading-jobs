apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenLocal()
    mavenCentral()
    mavenRepo name: 'datanucleus', url: 'http://www.datanucleus.org/downloads/maven2'
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
    mavenRepo name: 'sonatype', url: 'http://oss.sonatype.org/content/groups/public/'
    mavenRepo name: 'sonatype-snapshots', url: 'http://oss.sonatype.org/content/repositories/snapshots'
}

ext.cascadingVersion = '2.1.2'

configurations {
    all*.exclude group: 'org.mortbay.jetty'
    all*.exclude group: 'tomcat'
    cucumberRuntime {
        exclude group: 'commons-httpclient'
        extendsFrom testRuntime
    }
}

dependencies {
    compile group: 'org.apache.hadoop', name: 'hadoop-streaming', version: '1.0.4'
    compile group: 'org.apache.hadoop', name: 'hadoop-tools', version: '1.0.4'

    // the production code uses the slf4j logging api at compile time
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'cascading', name: 'cascading-core', version: '2.1.2'
    compile group: 'cascading', name: 'cascading-local', version: '2.1.2'
    compile group: 'cascading', name: 'cascading-hadoop', version: '2.1.2'
    compile (group: 'org.elasticsearch', name: 'elasticsearch-hadoop', version:'1.3.0.BUILD-SNAPSHOT') {
        exclude group: 'org.apache.pig'
        exclude group: 'org.apache.hive'
    }

    cucumberRuntime files("${jar.archivePath}")

    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.0.5'
    testCompile group: 'io.searchbox', name: 'jest', version: '0.0.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.1.2'
    testCompile group: 'info.cukes', name: 'cucumber-groovy', version: '1.1.2'
}

jar {
    description = "Assembles a Hadoop ready jar file"
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes("Main-Class": "com.viadeo.cascading/Main")
    }
}


task cucumber() {
    dependsOn assemble
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime
            args = ['-f', 'pretty', '--glue', 'src/test/groovy', 'src/test/resources']

        }
    }
}